apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdk 34

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "io.github.hidroh.materialistic"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 79
        versionName "3.3"
        buildConfigField "int", "LATEST_RELEASE", "77"
        buildConfigField "String", "GITHUB_TOKEN", "\"\""
        buildConfigField "String", "MERCURY_TOKEN", "\"\""
        proguardFiles getDefaultProguardFile('proguard-android.txt'),
                'proguard-rules.pro',
                'proguard-square.pro',
                'proguard-support.pro',
                'proguard-rx.pro'
        resConfigs 'en', 'zh-rCN', 'es'
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            // Use debug signing for release build
            signingConfig signingConfigs.debug
        }
    }
    lint {
        htmlReport false
        xmlReport false
        textReport true
        lintConfig file("${rootProject.getRootDir()}/lint.xml")
        abortOnError false
        explainIssues false
        absolutePaths false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        jvmToolchain(17)
    }
    namespace 'io.github.hidroh.materialistic'

}

ext {
    okHttpVersion = '3.9.1'
    daggerVersion = '1.2.5'
    retrofitVersion = '2.9.0'
    roomVersion = '2.5.1'
    lifecycleVersion = '2.6.1'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1',
            'androidx.recyclerview:recyclerview:1.3.0',
            'androidx.cardview:cardview:1.0.0',
            'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0',
            'com.google.android.material:material:1.8.0',
            'androidx.preference:preference:1.2.0',
            'androidx.browser:browser:1.5.0',
            "com.squareup.dagger:dagger:$daggerVersion",
            "com.squareup.retrofit2:retrofit:$retrofitVersion",
            "com.squareup.retrofit2:converter-gson:$retrofitVersion",
            "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion",
            "com.squareup.okhttp3:okhttp:$okHttpVersion",
            "com.squareup.okhttp3:logging-interceptor:$okHttpVersion",
            "io.reactivex:rxandroid:1.2.1",
            'io.reactivex:rxjava:1.3.8',
            "androidx.room:room-runtime:$roomVersion",
            "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion",
            "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion",
            "androidx.lifecycle:lifecycle-compiler:$lifecycleVersion",
            "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    kapt "androidx.room:room-compiler:$roomVersion",
            "com.squareup.dagger:dagger-compiler:$daggerVersion"
    kaptTest "androidx.room:room-compiler:$roomVersion",
            "com.squareup.dagger:dagger-compiler:$daggerVersion"
}
